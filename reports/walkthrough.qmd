---
title: "Hello, Quarto"
execute:
  eval: false
format: pdf
---

```{stata}
*Settings ----
clear all
set more off
set varabbrev off
version 17


*Imports ----
cd "D:\OneDrive\Documentos\Bristol\Economics (Bsc)\Applied Economics Dissertation\lippmann-replication"
use "data\raw\gsoep.dta", clear
//net install cleanplots, from("https://tdmize.github.io/data/cleanplots")
set scheme cleanplots


*Cleaning ----
/*
edu4 contains some -1 values, which I believe are missing.
whweek has some negative values, so I will remove them. Same with the incomes.

errand, hwork, ccare, repairs and hobbies all have missing variables.
Will not remove since not in the regression.

Not all households have two members in the data for a given year, so I remove
these. Same with homosexual couples.
 */
keep if whweek >= 0 & incjob1_mg > 0 & incjob1_mn > 0 //only dual earner couples here
drop if missing(hwork) | edu4 == -1 //other vars have missing, but not in regression

//keep only heterosexual couples with both members on the data
bysort wave cpf_hid (female): keep if _N == 2 & female[1] != female[2]

//remove singletons to maintain consistent sample size accross specifications
bysort pid: drop if _N == 1
drop if pid == 2088902

*Origin
bysort wave cpf_hid: drop if loc89[1] != loc89[2] //only single origin couples
bysort wave cpf_hid: gen east = (loc89[1] == 1 & loc89[2] == 1)

*Income
bysort wave cpf_hid: egen max_inc_mg = max(incjob1_mg)
bysort wave cpf_hid: egen max_inc_mn = max(incjob1_mn)

gen wife_earns_more = (female == 1 & incjob1_mg == max_inc_mg) //dummy if wife earns more than husband
replace wife_earns_more = 1 if female == 0 & incjob1_mg != max_inc_mg

*Income share
bysort wave cpf_hid: egen total_incjob1_mg = total(incjob1_mg)
bysort wave cpf_hid: egen hhd_inc = total(incjob1_mn) //household income uses net

gen female_income_share = incjob1_mn / hhd_inc if female == 1
bysort wave cpf_hid (female): replace female_income_share = female_income_share[2] if missing(female_income_share)


*Figure 2 ----
graph twoway kdensity female_income_share if female == 1 & east == 1, xline(0.5) name(graph1) title(East Germans) xlabel(#10) xtitle(Female Income Share) ytitle(Density)
graph twoway kdensity female_income_share if female == 1 & east == 0, xline(0.5) name(graph2) title(West Germans) xlabel(#10) xtitle(Female Income Share) ytitle(Density)

graph combine graph1 graph2, name(graph3)


*Figure 3 ----
preserve //this is not part of the replication, it is just a check
egen fem_share_cat = cut(female_income_share), at(0,0.1,0.2,0.3,0.4,0.5,0.6,0.7,0.8,0.9,1)
egen mean = mean(hwork), by(east female fem_share_cat)
graph twoway line mean fem_share_cat if east == 0 & female == 1, sort yscale(r(0 4))
graph twoway line mean fem_share_cat if east == 1 & female == 1, sort yscale(r(0 4))
graph twoway line mean fem_share_cat if east == 0 & female == 0, sort yscale(r(0 4))
graph twoway line mean fem_share_cat if east == 1 & female == 0, sort yscale(r(0 4))
restore


*Regression ----
*Additional dummies
bysort wave cpf_hid (female): gen p_age = cond(female == 0, age[2], age[1]) //partner age
bysort wave cpf_hid (female): gen p_edu4 = cond(female == 0, edu4[2], edu4[1]) //partner education
bysort wave cpf_hid (female): gen p_income = cond(female == 0, incjob1_mg[2], incjob1_mg[1]) //partner income
gen kids = (kidsn_hh17 != 0)

gen linc = log(incjob1_mg)
gen plinc = log(p_income)
gen lhhd_inc = log(hhd_inc)

*Housework gap
bysort wave cpf_hid: egen couple_hwork = total(hwork)
gen hwork_gap = 2*hwork - couple_hwork if female == 1
bysort wave cpf_hid (female): replace hwork_gap = hwork_gap[2] if missing(hwork_gap)

*NOTE: female vs male income share makes no different in all specifications

*define a string with all controls (this should all instead be in a function, but stata sucks)
local cross_sec_controls female_income_share lhhd_inc linc plinc c.age##c.age c.p_age##c.p_age kids i.edu4 i.p_edu4
local longitudinal_controls c.female_income_share##c.east lhhd_inc linc plinc c.age##c.age c.p_age##c.p_age kids i.edu4 i.p_edu4

*Panel A
eststo: reghdfe hwork wife_earns_more `cross_sec_controls' if female == 1 & east == 0, absorb(wavey state) vce(cluster pid) //(1)
eststo: reghdfe hwork wife_earns_more `cross_sec_controls' if female == 1 & east == 1, absorb(wavey state) vce(cluster pid) //(2)
eststo: reghdfe hwork wife_earns_more c.wife_earns_more#c.east `longitudinal_controls' if female == 1, absorb(wavey state) vce(cluster pid) //(3)

eststo: reghdfe hwork wife_earns_more `cross_sec_controls' if female == 1 & east == 0, absorb(wavey state pid) vce(cluster pid) //(4)
eststo: reghdfe hwork wife_earns_more `cross_sec_controls' if female == 1 & east == 1, absorb(wavey state pid) vce(cluster pid) //(5)
eststo: reghdfe hwork wife_earns_more c.wife_earns_more#c.east `longitudinal_controls' if female == 1, absorb(wavey state pid) vce(cluster pid) //(6)

esttab using "reports\figures\figure3.rtf", ///
b(%9.3f) se(%9.3f) star(* .10 ** .05 *** .01) ///
mtitles(West East All West East All) coeflabels(wife_earns_more WifeEarnsMore c.wife_earns_more#c.east WifeEarnsMore×East east East) nonotes ///
noomitted keep(wife_earns_more c.wife_earns_more#c.east east) ///
stats(N, fmt(0 0) label("Observations")) title("Panel A: Women --- Dependent Variable: Housework Time (hours per day)") ///
replace
eststo clear

*Panel B
local cross_sec_controls female_income_share lhhd_inc linc plinc c.age##c.age c.p_age##c.p_age kids i.edu4 i.p_edu4
local longitudinal_controls c.female_income_share##c.east lhhd_inc linc plinc c.age##c.age c.p_age##c.p_age kids i.edu4 i.p_edu4

eststo: reghdfe hwork wife_earns_more `cross_sec_controls' if female == 0 & east == 0, absorb(wavey state) vce(cluster pid) //(1)
eststo: reghdfe hwork wife_earns_more `cross_sec_controls' if female == 0 & east == 1, absorb(wavey state) vce(cluster pid) //(2)
eststo: reghdfe hwork wife_earns_more c.wife_earns_more#c.east `longitudinal_controls' if female == 0, absorb(wavey state) vce(cluster pid) //(3)

eststo: reghdfe hwork wife_earns_more `cross_sec_controls' if female == 0 & east == 0, absorb(wavey state pid) vce(cluster pid) //(4)
eststo: reghdfe hwork wife_earns_more `cross_sec_controls' if female == 0 & east == 1, absorb(wavey state pid) vce(cluster pid) //(5)
eststo: reghdfe hwork wife_earns_more c.wife_earns_more#c.east `longitudinal_controls' if female == 0, absorb(wavey state pid) vce(cluster pid) //(6)

esttab using "reports\figures\figure3.rtf", ///
b(%9.3f) se(%9.3f) star(* .10 ** .05 *** .01) ///
nomtitles nonumbers coeflabels(wife_earns_more WifeEarnsMore c.wife_earns_more#c.east WifeEarnsMore×East east East) nonotes ///
noomitted keep(wife_earns_more c.wife_earns_more#c.east east) ///
stats(N, fmt(0 0) label("Observations")) title("Panel B: Men --- Dependent Variable: Housework Time (hours per day)") ///
append
eststo clear

*Panel C
eststo: reghdfe hwork_gap wife_earns_more `cross_sec_controls' if female == 0 & east == 0, absorb(wavey state) vce(cluster pid) //(1)
estadd local fe "No"
estadd scalar individuals = e(N_clust)
eststo: reghdfe hwork_gap wife_earns_more `cross_sec_controls' if female == 0 & east == 1, absorb(wavey state) vce(cluster pid) //(2)
estadd local fe "No"
estadd scalar individuals = e(N_clust)
eststo: reghdfe hwork_gap wife_earns_more c.wife_earns_more#c.east `longitudinal_controls' if female == 0, absorb(wavey state) vce(cluster pid) //(3)
estadd local fe "No"
estadd scalar individuals = e(N_clust)

eststo: reghdfe hwork_gap wife_earns_more `cross_sec_controls' if female == 0 & east == 0, absorb(wavey state pid) vce(cluster pid) //(4)
estadd local fe "Yes"
estadd scalar individuals = e(N_clust)
eststo: reghdfe hwork_gap wife_earns_more `cross_sec_controls' if female == 0 & east == 1, absorb(wavey state pid) vce(cluster pid) //(5)
estadd local fe "Yes"
estadd scalar individuals = e(N_clust)
eststo: reghdfe hwork_gap wife_earns_more c.wife_earns_more#c.east `longitudinal_controls' if female == 0, absorb(wavey state pid) vce(cluster pid) //(6)
gen used = e(sample)
estadd local fe "Yes"
estadd scalar individuals = e(N_clust)

esttab using "reports\figures\figure3.rtf", ///
b(%9.3f) se(%9.3f) star(* .10 ** .05 *** .01) ///
nomtitles nonumbers coeflabels(wife_earns_more WifeEarnsMore c.wife_earns_more#c.east WifeEarnsMore×East east East) ///
noomitted keep(wife_earns_more c.wife_earns_more#c.east east) ///
stats(N individuals fe, fmt(0 0) label("Observations" "Individuals" "Individual fixed effects")) title("Panel C: Couple --- Dependent Variable: Housework Time Gap (Woman's - Man's)") ///
append
```
